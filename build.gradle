plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.20'
    id 'org.jetbrains.grammarkit' version '2020.2.1'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.5.10'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.7'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen'
            srcDir 'src/main/java'
        }
    }
}    
    
intellij {
    version '2020.1.3'
    pluginName 'kick-assembler-acbg'
    updateSinceUntilBuild false
}

publishPlugin {
    token intellijPublishToken
}

group 'de.achimonline'
version 1.3

intellij.plugins 'java'

apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.*

task genLexer(type: GenerateLexer) {
    source = 'src/main/java/de/achimonline/kickassembler/acbg/lexer/KickAssembler.flex'
    targetDir = 'src/main/gen/de/achimonline/kickassembler/acbg/lexer/'
    targetClass = 'KickAssemblerLexer'
    purgeOldFiles = true
}

task genParser(type: GenerateParser) {
    source = 'src/main/java/de/achimonline/kickassembler/acbg/psi/KickAssembler.bnf'
    targetRoot = 'src/main/gen'
    pathToParser = '/de/achimonline/kickassembler/acbg/parser/KickAssemblerParser.java'
    pathToPsiRoot = '/de/achimonline/kickassembler/acbg/psi'
    purgeOldFiles = true
}

clean.doFirst {
    delete "${rootDir}/src/test/resources/parser/*.sym"
    delete "${rootDir}/src/test/resources/parser/*.prg"
    delete "${rootDir}/src/test/resources/parser/asminfo.txt"
    delete "${rootDir}/src/test/resources/parser/ByteDump.txt"
    delete "${rootDir}/src/test/resources/parser/DeleteMe.bin"
}
